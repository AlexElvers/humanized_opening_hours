WDAY : "Mo" | "Tu" | "We" | "Th" | "Fr" | "Sa" | "Su"
MONTH : "Jan" | "Feb" | "Mar" | "Apr" | "May" | "Jun" | "Jul" | "Aug" | "Sep" | "Oct" | "Nov" | "Dec"
PUBLIC_HOLIDAY : "PH"
SCHOOL_HOLIDAY : "SH"
EVENT : "dawn" | "sunrise" | "sunset" | "dusk"
EASTER : "easter"
OPEN : " open"
CLOSED : " closed" | " off"
ALWAYS_OPEN.2 : "24/7"
WEEK.2 : "week "

TWO_DIGITS.1 : /[0-9][0-9]+/
PLUS_OR_MINUS : "+" | "-"

time_domain : rule_sequence ("; " rule_sequence)*

rule_sequence : selector_sequence [rule_modifier]
              | rule_modifier

rule_modifier : OPEN -> rule_modifier_open
              | CLOSED -> rule_modifier_closed


selector_sequence : ALWAYS_OPEN -> always_open
                  | range_selectors [" " time_selector]
                  | time_selector

range_selectors : [monthday_selector] [week_selector] [":"] [" "] [weekday_selector]
//range_selectors : [year_selector] [monthday_selector] [week_selector] [":"] [" "] [weekday_selector]

weekday_selector : (weekday_sequence | holiday ("," holiday)*) -> weekday_or_holiday_sequence_selector
                 | holiday ("," holiday)* "," weekday_sequence -> holiday_and_weekday_sequence_selector
                 | weekday_sequence "," holiday ("," holiday)* -> holiday_and_weekday_sequence_selector  // Not valid but frequent pattern.
                 | holiday ("," holiday)* " " weekday_sequence -> holiday_in_weekday_sequence_selector

weekday_sequence : weekday_range ("," weekday_range)*
weekday_range : WDAY
              | WDAY "-" WDAY

holiday : PUBLIC_HOLIDAY
        | SCHOOL_HOLIDAY

%import common.INT
//day_offset : " " PLUS_OR_MINUS INT " day" ["s"]

time_selector : timespan ("," timespan)*
timespan : time "-" time
time : hour_minutes | variable_time
hour_minutes : TWO_DIGITS ":" TWO_DIGITS
variable_time : EVENT
              | "(" EVENT PLUS_OR_MINUS hour_minutes ")"

year_selector : year_range ("," year_range)*
year_range : year
           | year "-" year
           | year "-" year "/" INT
year : /([12][0-9]{3})+/

monthday_selector : monthday_range ("," monthday_range)*
monthday_range : [year] MONTH ["-" MONTH] -> monthday_range_year_month
               | date_from -> monthday_range_date_from  //TODO : "+" version
               | date_from "-" date_to -> monthday_range_date_from_to
date_from : [year " "] MONTH " " INT
          | [year " "] variable_date //[day_offset]  //TODO
date_to : date_from
        | INT
variable_date : EASTER

week_selector : WEEK week ("," week)*  // TODO
week : weeknum
     | weeknum "-" weeknum
     | weeknum "-" weeknum "/" INT
weeknum : INT
